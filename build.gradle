import java.text.SimpleDateFormat

buildscript {
    ext.kotlinVersion = '1.2.31'
    ext.gradleRioVersion = '2018.03.06'
    ext.sertainVersion = '1.2.0'
    ext.ktlintVersion = '0.19.0'
    ext.gsonVersion = '2.8.5'

    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "gradle.plugin.jaci.openrio.gradle:GradleRIO:$gradleRioVersion"

    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jaci.openrio.gradle.GradleRIO'
tasks.whenTaskAdded { task ->
    if (task.name == 'deploy' || task.name == 'deployMain') task.dependsOn 'assemble'
}
check.dependsOn 'ktlint'
build.dependsOn 'versionTxt'

repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
}

configurations {
    ktlint
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    compile "org.sert2521.sertain:core:$sertainVersion"
    compile "com.google.code.gson:gson:$gsonVersion"

    compile wpilib()
    compile navx()
    compile ctre()
    compile pathfinder()
    compile openrio.powerup.matchData()

    ktlint "com.github.shyiko:ktlint:$ktlintVersion"
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

deploy {
    targets {
        target('roborio', jaci.openrio.gradle.frc.RoboRIO) {
            team = 2521
            addresses << '10.25.21.2'
        }
    }

    artifacts {
        artifact('main', jaci.openrio.gradle.frc.FRCJavaArtifact) {
            targets << 'roborio'

            debug = false
        }
    }
}

wpi {
    wpilibVersion = '2018.4.1'
    ntcoreVersion = '4.1.0'
    opencvVersion = '3.2.0'
    cscoreVersion = '1.3.0'
    wpiutilVersion = '3.2.0'

    ctreVersion = '5.3.1.0'
    navxVersion = '3.0.346'

    shuffleboardVersion = '1.3.1'
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest('org.sert2521.powerup.Poe')
}

task ktlint(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args 'src/**/*.kt'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitBranch = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitFilesChanged = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'diff', '--name-only', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim().replace('\n', ', ')
}

task versionTxt()  {
    doLast {
        String resourcesDir = "$projectDir/src/main/resources"
        new File("$resourcesDir/branch.txt").text = getGitBranch()
        new File("$resourcesDir/commit.txt").text = getGitHash()
        new File("$resourcesDir/changes.txt").text = getGitFilesChanged()
        new File("$resourcesDir/buildtime.txt").text =
                new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())
    }
}
