buildscript {
    ext.kotlinVersion = '1.2.30'
    ext.gradleRioVersion = '2018.02.17'
    ext.sertainVersion = 'c1a751504d'
    ext.ktlintVersion = '0.19.0'

    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "gradle.plugin.jaci.openrio.gradle:GradleRIO:$gradleRioVersion"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jaci.openrio.gradle.GradleRIO'
tasks.whenTaskAdded { task ->
    if (task.name == 'deploy') task.dependsOn 'assemble'
}
check.dependsOn 'ktlint'

repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
}

configurations {
    ktlint
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    compile "org.sert2521.sertain:core:$sertainVersion"

    compile wpilib()
    compile navx()
    compile ctre()
    compile pathfinder()
    compile openrio.powerup.matchData()

    ktlint "com.github.shyiko:ktlint:$ktlintVersion"
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

deploy {
    targets {
        target('roborio', jaci.openrio.gradle.frc.RoboRIO) {
            team = 2521
            addresses << '10.25.21.2'
        }
    }

    artifacts {
        artifact('main', jaci.openrio.gradle.frc.FRCJavaArtifact) {
            targets << 'roborio'

            debug = false
        }
    }
}

wpi {
    wpilibVersion = '2018.4.1'
    ntcoreVersion = '4.0.0'
    opencvVersion = '3.2.0'
    cscoreVersion = '1.2.0'
    wpiutilVersion = '3.1.0'

    ctreVersion = '5.3.1.0'
    navxVersion = '3.0.346'

    smartDashboardVersion = '3.0.0'
    shuffleboardVersion = '1.2.1'
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest('org.sert2521.powerup.Poe')
}

task ktlint(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args 'src/**/*.kt'
}
