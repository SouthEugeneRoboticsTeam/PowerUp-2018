buildscript {
    ext.kotlinVersion = "1.2.20"

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "gradle.plugin.jaci.openrio.gradle:GradleRIO:2018.01.11"
    }
}

apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "jaci.openrio.gradle.GradleRIO"
tasks.whenTaskAdded { task ->
    if (task.name == "deploy") task.dependsOn 'assemble'
}
check.dependsOn "ktlint"

repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
}

configurations {
    ktlint
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    compile "org.sert2521.sertain:core:d9c87c5cef"

    compile wpilib()
    compile navx()
    compile ctre()
    compile pathfinder()

    ktlint 'com.github.shyiko:ktlint:0.15.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

deploy {
    targets {
        target('roborio', jaci.openrio.gradle.frc.RoboRIO) {
            team = 2521
            addresses << '10.25.21.89'
        }
    }

    artifacts {
        artifact('main', jaci.openrio.gradle.frc.FRCJavaArtifact) {
            targets << 'roborio'

            debug = false
        }
    }
}

// Latest versions:
// https://github.com/Open-RIO/GradleRIO/tree/\
// 34b602a105703fa4635398b12e824fc2a70041d9/src/main/groovy/jaci/openrio/gradle/wpi/dependencies
wpi {
    wpilibVersion = '2018.2.1'
    ntcoreVersion = '4.0.0'
    opencvVersion = '3.2.0'
    cscoreVersion = '1.1.0'
    wpiutilVersion = '3.0.0'

    ctreVersion = '5.1.2.1'
    navxVersion = '3.0.346'

    smartDashboardVersion = '2.0.4'
    shuffleboardVersion = '1.0.0'
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest('org.sert2521.powerup.RobotName')
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
